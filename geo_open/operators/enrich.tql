---
description: |
  Enriches OCSF Network Activity events with geolocation data from Geo Open.
args:
  named:
    - name: asn
      type: bool
      default: false
      description: Include ASN (Autonomous System Number) information in addition to country.
---

// NB: We call context::enrich outside the `if` block because putting it inside
// triggers an assertion failure in if.cpp:567. Using `key=src_endpoint?.ip`
// handles null keys gracefully.

let $ctx = "geo-open-country-asn" if $asn else "geo-open-country"

context::enrich $ctx, key=src_endpoint?.ip, into=_src_geo
if _src_geo != null and _src_geo.country.iso_code != "None" {
  src_endpoint.location.country = _src_geo.country.iso_code
}
if $asn and _src_geo != null {
  src_endpoint.autonomous_system = {
    name: _src_geo.country.AutonomousSystemOrganization,
    number: _src_geo.country.AutonomousSystemNumber.int(),
  }
}
drop _src_geo

context::enrich $ctx, key=dst_endpoint?.ip, into=_dst_geo
if _dst_geo != null and _dst_geo.country.iso_code != "None" {
  dst_endpoint.location.country = _dst_geo.country.iso_code
}
if $asn and _dst_geo != null {
  dst_endpoint.autonomous_system = {
    name: _dst_geo.country.AutonomousSystemOrganization,
    number: _dst_geo.country.AutonomousSystemNumber.int(),
  }
}
drop _dst_geo
