---
description: |
  Enriches OCSF Network Activity events with country and ASN information from Geo Open data.
---

// NB: We call context::enrich outside the `if` block because putting it inside
// triggers an assertion failure in if.cpp:567. Using `key=src_endpoint?.ip`
// handles null keys gracefully.

context::enrich "geo-open-country-asn", key=src_endpoint?.ip, into=_src_geo
if _src_geo != null {
  if _src_geo.country.iso_code != "None" {
    src_endpoint.location.country = _src_geo.country.iso_code
  }
  src_endpoint.autonomous_system = {
    name: _src_geo.country.AutonomousSystemOrganization,
    number: _src_geo.country.AutonomousSystemNumber.int(),
  }
}
drop _src_geo

context::enrich "geo-open-country-asn", key=dst_endpoint?.ip, into=_dst_geo
if _dst_geo != null {
  if _dst_geo.country.iso_code != "None" {
    dst_endpoint.location.country = _dst_geo.country.iso_code
  }
  dst_endpoint.autonomous_system = {
    name: _dst_geo.country.AutonomousSystemOrganization,
    number: _dst_geo.country.AutonomousSystemNumber.int(),
  }
}
drop _dst_geo
