// --- Preamble ---------------------------------

this = { suricata: this }
suricata::ocsf::map_common

// --- OCSF: classification attributes ----------

ocsf.activity_id = 6

ocsf.category_uid = 4
ocsf.class_uid = 4001
ocsf.severity_id = 1
ocsf.type_uid = ocsf.class_uid * 100 + ocsf.activity_id

// --- OCSF: context attributes -----------------

ocsf.tls = {
  ja3_hash: {
    algorithm_id: 0,
    value: move suricata.tls.ja3?.hash?,
  },
  ja3s_hash: {
    algorithm_id: 0,
    value: move suricata.tls.ja3s?.hash?,
  },
  server_name: move suricata.tls.sni?,
  version: move suricata.tls.version? else "Unknown",
}

ocsf.tls.certificate = {
  creation_time: move suricata.tls.notbefore?,
  expiration_time: move suricata.tls.notafter?,
  issuer: move suricata.tls.issuerdn?,
  serial_number: move suricata.tls.serial?,
  subject: move suricata.tls.subject?,
}

if suricata.tls.has("fingerprint") {
  ocsf.tls.certificate.fingerprints = [{
    algorithm_id: 2,
    value: suricata.tls.fingerprint.replace(":", ""),
  }]
  drop suricata.tls.fingerprint
}

// --- OCSF: primary attributes -----------------

ocsf.status_id = 0

drop_null_fields

// --- Finalize ---------------------------------

this = {...ocsf, unmapped: suricata}
@name = "ocsf.network_activity"
ocsf::derive