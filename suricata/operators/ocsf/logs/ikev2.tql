// TODO: support frontmatter in UDOs.
//---
//description: |
//  Maps Suricata IKEv2 events to OCSF Network Activity events.
//---

// --- Preamble ---------------------------------

this = { suricata: this }
suricata::ocsf::map_common

// --- OCSF: classification attributes ----------

ocsf.activity_id = 6
ocsf.category_uid = 4
ocsf.class_uid = 4001
ocsf.severity_id = 1
ocsf.type_uid = 400106

// --- OCSF: occurrence attributes --------------

ocsf.time = move suricata.timestamp

// --- OCSF: context attributes -----------------

ocsf.metadata = {
  log_name: move suricata.event_type,
  product: {
    name: "Suricata",
    vendor_name: "Open Information Security Foundation",
  },
  uid: string(move suricata.flow_id),
  version: "1.4.0",
}

// --- OCSF: primary attributes -----------------

ocsf.dst_endpoint = {
  ip: move suricata.dest_ip,
  port: move suricata.dest_port,
}
ocsf.src_endpoint = {
  ip: move suricata.src_ip,
  port: move suricata.src_port,
}
if suricata.has("flow") and suricata.flow != null {
  ocsf.traffic = {
    bytes_in: move suricata.flow.bytes_toclient,
    bytes_out: move suricata.flow.bytes_toserver,
    packets_in: move suricata.flow.pkts_toclient,
    packets_out: move suricata.flow.pkts_toserver,
    bytes: move suricata.flow.bytes_toclient + move suricata.flow.bytes_toserver,
    packets: move suricata.flow.pkts_toclient + move suricata.flow.pkts_toserver,
  }
  if suricata.flow.has("state") {
    ocsf.status_id = 99
    ocsf.status = move suricata.flow.state
  } else {
    ocsf.status_id = 0
    ocsf.status = "Unknown"
  }
} else {
  ocsf.traffic = null
  ocsf.status_id = 0
  ocsf.status = "Unknown"
}

// --- Finalize ---------------------------------

this = {...ocsf, unmapped: suricata}
@name = "ocsf.network_activity"
