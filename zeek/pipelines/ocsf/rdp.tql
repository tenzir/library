---
name: rdp.log to OCSF RDP Activity
description: |
  Maps Zeek's rdp.log to OCSF RDP Activity events.
---

let $results = {
  "encrypted": 0,
  "Success": 1,
  "User rejected": 2,
  "Resources not available": 2,
  "Rejected for symmetry breaking": 2,
  "Locked conference": 2,
}
subscribe "zeek"
where @name == "zeek.rdp"
this = { zeek: this }
// === Classification ===
ocsf.activity_id = 6
ocsf.activity_name = "Traffic"
ocsf.category_uid = 4
ocsf.category_name = "Network Activity"
ocsf.class_uid = 4005
ocsf.class_name = "RDP Activity"
ocsf.severity_id = 1
ocsf.severity = "Informational"
ocsf.type_uid = ocsf.class_uid * 100 + ocsf.activity_id
// === Occurrence ===
move ocsf.time = zeek.ts
// === Context ===
ocsf.metadata = {
  log_name: "rdp.log",
  logged_time: move zeek._write_ts?,
  product: {
    name: "Zeek",
    vendor_name: "Zeek",
    cpe_name: "cpe:2.3:a:zeek:zeek",
  },
  profiles: ["host"],
  uid: move zeek.uid,
  version: "1.4.0",
}
drop zeek._path? // implied in metadata.log_name
move ocsf.protocol_ver = zeek.client_build?
move ocsf.identifier_cookie = zeek.cookie
ocsf.keyboard_info = {
  keyboard_layout: move zeek.keyboard_layout?,
}
// === Primary ===
ocsf.src_endpoint = {
  ip: zeek.id.orig_h,
  port: zeek.id.orig_p,
}
ocsf.dst_endpoint = {
  ip: zeek.id.resp_h,
  port: zeek.id.resp_p,
}
ocsf.connection_info = {
  protocol_name: "tcp",
  protocol_num: 6,
}
if zeek.id.orig_h.is_v6() or zeek.id.resp_h.is_v6() {
  ocsf.connection_info.protocol_ver_id = 6
} else {
  ocsf.connection_info.protocol_ver_id = 4
}
drop zeek.id
ocsf.display = {
  physical_height: move zeek.desktop_height?,
  physical_width: move zeek.desktop_width?,
  color_depth: int(replace(move zeek.requested_color_depth?, "bit", "")),
}
ocsf.tls = {
  key_length: int(replace(move zeek.encryption_method?, "bit", "")),
  // The version is required, but not present in the data.
  version: null,
}
ocsf.status = zeek.result
ocsf.status_id = $results[move zeek.result] else 0
// === Profile: Host ===
ocsf.host = {
  name: move zeek.client_name?,
  os: {
    name: "Windows",
    vendor_name: "Microsoft",
    cpe_name: "cpe:2.3:o:microsoft:windows",
    type_id: 100,
    type: "Windows",
  },
  // 64-byte clientDigProductId
  uid: move zeek.client_dig_product_id?,
  type_id: 2,
  type: "Desktop",
}
this = {...ocsf, unmapped: zeek}
@name = "ocsf.rdp_activity"
publish "ocsf"
