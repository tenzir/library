---
name: http.log to OCSF HTTP Activity
description: |
  Maps Zeek's http.log to OCSF HTTP Activity events.
---

let $methods = {
  CONNECT: 1,
  DELETE: 2,
  GET: 3,
  HEAD: 4,
  OPTIONS: 5,
  POST: 6,
  PUT: 7,
  TRACE: 8,
}
subscribe "zeek"
where @name == "zeek.http"
this = { zeek: this }
// === Classification ===
ocsf.activity_id = $methods[zeek.method]? else 0
ocsf.activity_name = (move zeek.method?).to_title()
ocsf.category_uid = 4
ocsf.category_name = "Network Activity"
ocsf.class_uid = 4002
ocsf.class_name = "HTTP Activity"
ocsf.severity_id = 1
ocsf.severity = "Informational"
ocsf.type_uid = ocsf.class_uid * 100 + ocsf.activity_id
// === Occurrence ===
move ocsf.time = zeek.ts
ocsf.start_time = ocsf.time
// === Context ===
ocsf.metadata = {
  log_name: "http.log",
  logged_time: move zeek._write_ts?,
  product: {
    name: "Zeek",
    vendor_name: "Zeek",
    cpe_name: "cpe:2.3:a:zeek:zeek",
  },
  uid: move zeek.uid,
  version: "1.4.0",
}
drop zeek._path? // implied in metadata.log_name
// === Primary ===
if zeek.has("dest_host") {
  host = zeek.dest_host
} else {
  host = zeek.host
}
ocsf.http_request = {
    http_method: ocsf.activity_name,
    referrer: move zeek.referrer,
    url: {
      hostname: move host,
      // TODO: Zeek's uri field is actually the path plus all
      // parameters. Take this apart with string or URL functions.
      path: move zeek.uri,
    },
    user_agent: move zeek.user_agent,
    version: move zeek.version,
}
ocsf.http_response = {
  code: move zeek.status_code,
  status: move zeek.status_msg,
}
ocsf.src_endpoint = {
  ip: zeek.id.orig_h,
  port: zeek.id.orig_p,
}
ocsf.dst_endpoint = {
  ip: zeek.id.resp_h,
  port: zeek.id.resp_p,
}
ocsf.connection_info = {
  direction: "Other",
  direction_id: 99,
  protocol_name: "tcp",
  protocol_num: 6,
}
if zeek.id.orig_h.is_v6() or zeek.id.resp_h.is_v6() {
  ocsf.connection_info.protocol_ver_id = 6
} else {
  ocsf.connection_info.protocol_ver_id = 4
}
drop zeek.id
ocsf.status = "Unknown"
ocsf.status_id = 0
this = {...ocsf, unmapped: zeek}
@name = "ocsf.http_activity"
publish "ocsf"
