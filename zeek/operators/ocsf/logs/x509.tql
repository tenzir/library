// TODO: support frontmatter in UDOs.
//---
//description: |
//  Maps Zeek's x509.log to OCSF Network Activity events.
//---

// --- Preamble ---------------------------------

this = { zeek: this }
zeek::ocsf::map_common

// --- OCSF: classification attributes ----------

ocsf.activity_id = 6
ocsf.activity_name = "Traffic"
ocsf.class_uid = 4001
ocsf.class_name = "Network Activity"
ocsf.severity_id = 1
ocsf.severity = "Informational"
ocsf.type_uid = ocsf.class_uid * 100 + ocsf.activity_id

// --- OCSF: context attributes -----------------

ocsf.metadata.log_name = "x509.log"

ocsf.tls = {
  certificate: {
    serial_number: move zeek.certificate.serial,
    created_time: move zeek.certificate.not_valid_before,
    expiration_time: move zeek.certificate.not_valid_after,
    issuer: move zeek.certificate.issuer,
    subject: move zeek.certificate.subject,
    // NB: [null].collect() yields the empty array.
    fingerprints: [move zeek.fingerprint?].collect(),
    version: (move zeek.certificate.version).string(),
  },
  sans: (move zeek.san.dns).map(name => {
    name: name,
    type: "DNSName",
  }),
  version: null,
}

// --- Finalize ---------------------------------

this = {...ocsf, unmapped: zeek}
@name = "ocsf.network_activity"
