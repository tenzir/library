---
name: Enrich OCSF events with OSINT objects
description: |
  Enriches OCSF events and adds the OSINT profile.
---

let $input = "{{ inputs.enrich-input-topic }}"
let $output = "{{ inputs.enrich-output-topic }}"
subscribe $input
if category_uid == 4 { // Network Activity
  if src_endpoint?.ip? != null {
    context::enrich "ocsf-osint",
      key=src_endpoint.ip,
      into=osint,
      mode="append"
  }
  if dst_endpoint?.ip? != null {
    context::enrich "ocsf-osint",
      key=dst_endpoint.ip,
      into=osint,
      mode="append"
  }
  if class_uid == 4003 and query?.hostname? != null { // DNS Activity
    context::enrich "ocsf-osint",
      key=query.hostname,
      into=osint,
      mode="append"
  }
}
// If we have the osint field, the enrichment succeeded.
if this.has("osint") {
  metadata.profiles = metadata?.profiles?.add("osint")
}
publish $output
